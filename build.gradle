plugins {
    id 'java-library'
    id 'maven-publish'
    alias(libs.plugins.jreleaser)
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

group = 'com.exaroton'
version = project.findProperty("release") ?: "dev"
ext.isReleaseVersion = version != "dev" &&  !version.endsWith("SNAPSHOT")

repositories {
    mavenCentral()
}

dependencies {
    compileOnly(libs.jetbrains.annotations)

    implementation(libs.gson)
    implementation(libs.slf4j.api)


    testCompileOnly(libs.jetbrains.annotations)

    testImplementation(libs.junit)
    testImplementation(libs.slf4j.jdk14)

    testRuntimeOnly(libs.junit.platform.launcher)
}

test {
    def trustStorePath = System.getenv('JAVA_TRUSTSTORE')
    if (trustStorePath) {
        jvmArgs "-Djavax.net.ssl.trustStore=${trustStorePath}"
    }
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

java {
    withJavadocJar()
    withSourcesJar()

    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'api'
            from components.java
            pom {
                name = 'exaroton API'
                description = 'The official exaroton java library'
                url = 'https://github.com/exaroton/java-exaroton-api'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/exaroton/java-exaroton-api/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'julian'
                        name = 'Julian Vennen'
                        email = 'julian@aternos.org'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/exaroton/java-exaroton-api.git'
                    developerConnection = 'scm:git:git@github.com:exaroton/java-exaroton-api.git'
                    url = 'https://github.com/exaroton/java-exaroton-api'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    signing {
        active = 'RELEASE'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                'release-deploy' {
                    active = 'RELEASE'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
            nexus2 {
                'snapshot-deploy' {
                    active = 'SNAPSHOT'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    snapshotUrl = 'https://central.sonatype.com/repository/maven-snapshots'
                    snapshotSupported = true
                    closeRepository = true
                    releaseRepository = true
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }

    release {
        github {
            skipRelease = true
        }
    }
}

jar {
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}
